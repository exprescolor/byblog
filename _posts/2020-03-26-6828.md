---
layout:     post   				    # 使用的布局（不需要改）
title:      828-lab1-part2		# 标题 
#subtitle:   脚本，xss #副标题
date:       2020-03-26 				# 时间
author:     s-seven 						# 作者
header-img: img/post-bg-2015.jpg 	#这篇文章标题背景图片
catalog: true 						# 是否归档
tags:								#标签
    - 6828
---



#Part 2: The Boot Loader

**扇区**：个人计算机的软盘和硬盘被划分为512字节的区域，称为扇区。扇区是磁盘的最小传输粒度:每个读或写操作的大小必须是一个或多个扇区，并且在扇区边界上对齐。如果磁盘是可引导的，则第一个扇区称为引导扇区，因为这是引导加载程序代码所在的位置。

当BIOS找到可引导的软盘或硬盘时，它将512字节的引导扇区装入物理地址0x7c00到0x7dff的内存中，然后使用jmp指令将CS:IP设置为0000:7c00，将控制权交给引导加载程序。与BIOS加载地址一样，这些地址是相当随意的—但是它们是固定的，并且对于pc来说是标准化的。

从CD-ROM引导的能力在PC的发展过程中出现得很晚，因此PC架构师抓住机会重新思考了一下引导过程。因此，现代BIOS从CD-ROM引导的方式有点复杂(也更强大)。cd - rom使用2048字节而不是512字节的扇区大小，BIOS可以在将控制转移到内存之前将更大的引导映像从磁盘加载到内存(而不仅仅是一个扇区)。

然而，对于6.828，我们将使用传统的硬盘驱动器引导机制，这意味着我们的引导加载程序必须小于512字节。boot loader由一个汇编语言源文件(boot/boot.s)和一个C源文件(boot/main.c)组成。

boot loader必须执行两个主要功能：

1. boot loader将处理器从实际模式切换到32位保护模式，因为只有在这种模式下，软件才能访问处理器物理地址空间中所有大于1MB的内存。转换偏移量之后是32位而不是16位。

2. boot loader通过x86的特殊I/O指令直接访问IDE磁盘设备寄存器，从而从硬盘读取内核。

理解boot loader源代码之后，查看文件obj/boot/boot.asm。这个文件是我们的GNUmakefile在编译引导加载程序之后创建的引导加载程序的一个反汇编。这个反汇编文件可以很容易地查看物理内存中所有引导加载程序的代码所在的位置，并且可以更容易地跟踪在GDB中单步执行引导加载程序时发生的情况。

## 练习3

> 请参阅实验室工具指南，特别是关于GDB命令的部分。熟悉GDB，这也包括一些对操作系统工作有用的深奥的GDB命令。
>
> 在地址0x7c00处设置一个断点，引导扇区将在这里加载。继续执行，直到该断点。跟踪boot/boot.s中的代码。使用源代码和反汇编文件obj/boot/boot.asm跟踪您的位置。还可以使用GDB中的x/i命令在引导加载程序中对指令序列进行反汇编，并将原始的引导加载程序源代码与obj/boot/boot.asm中的反汇编进行比较和GDB。
>
> 跟踪到boot/main中的bootmain().c，然后进入readsect()。识别与readsect()中的每个语句对应的确切的程序集指令。跟踪readsect()的其余部分，然后返回bootmain()，并标识从磁盘读取内核其余扇区的for循环的开始和结束。找出循环结束时将运行什么代码，在那里设置一个断点，然后继续运行该断点。然后逐步完成引导加载程序的其余部分。

###boot loader的组成

先看一下boot.S文件：

```assembly
#include <inc/mmu.h>

# Start the CPU: switch to 32-bit protected mode, jump into C.
# The BIOS loads this code from the first sector of the hard disk into
# memory at physical address 0x7c00 and starts executing in real mode
# with %cs=0 %ip=7c00.

.set PROT_MODE_CSEG, 0x8         # kernel code segment selector
.set PROT_MODE_DSEG, 0x10        # kernel data segment selector
.set CR0_PE_ON,      0x1         # protected mode enable flag

.globl start
start:
  .code16                     # Assemble for 16-bit mode
  cli                         # Disable interrupts
  cld                         # String operations increment

  # Set up the important data segment registers (DS, ES, SS).
  xorw    %ax,%ax             # Segment number zero
  movw    %ax,%ds             # -> Data Segment
  movw    %ax,%es             # -> Extra Segment
  movw    %ax,%ss             # -> Stack Segment

  # Enable A20:
  #   For backwards compatibility with the earliest PCs, physical
  #   address line 20 is tied low, so that addresses higher than
  #   1MB wrap around to zero by default.  This code undoes this.
seta20.1:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.1

  movb    $0xd1,%al               # 0xd1 -> port 0x64
  outb    %al,$0x64
;D1指令代表下一次写入0x60端口的数据将被写入给804x控制器的输出端口。可以理解为下一个写入0x60端口的数据是一个控制指令。
seta20.2:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.2

  movb    $0xdf,%al               # 0xdf -> port 0x60
  outb    %al,$0x60
;DF的含义可以从图中看到，使能A20线，代表可以进入保护模式了。

  # Switch from real to protected mode, using a bootstrap GDT从实际模式切换到保护模式
  # and segment translation that makes virtual addresses 
  # identical to their physical addresses, so that the 
  # effective memory map does not change during the switch.
  lgdt    gdtdesc
  movl    %cr0, %eax
  orl     $CR0_PE_ON, %eax
  movl    %eax, %cr0
  
  # Jump to next instruction, but in 32-bit code segment.
  # Switches processor into 32-bit mode.
  ljmp    $PROT_MODE_CSEG, $protcseg

  .code32                     # Assemble for 32-bit mode
protcseg:
  # Set up the protected-mode data segment registers
  movw    $PROT_MODE_DSEG, %ax    # Our data segment selector
  movw    %ax, %ds                # -> DS: Data Segment
  movw    %ax, %es                # -> ES: Extra Segment
  movw    %ax, %fs                # -> FS
  movw    %ax, %gs                # -> GS
  movw    %ax, %ss                # -> SS: Stack Segment
  
  # Set up the stack pointer and call into C.
  movl    $start, %esp
  call bootmain

  # If bootmain returns (it shouldn't), loop.
spin:
  jmp spin

# Bootstrap GDT
.p2align 2                                # force 4 byte alignment
gdt:
  SEG_NULL				# null seg
  SEG(STA_X|STA_R, 0x0, 0xffffffff)	# code seg
  SEG(STA_W, 0x0, 0xffffffff)	        # data seg
;xv6并没有使用分段机制，也就是说数据和代码都是写在一起的，所以数据段和代码段的起始地址都是0x0，大小都是0xffffffff=4GB。
gdtdesc:
  .word   0x17                            # sizeof(gdt) - 1
  .long   gdt                             # address gdt
```

上面的代码进行了一些关闭中断，寄存器的初始化，保护模式，端口的设置等一些操作，这些操作结束后，会根据call bootmain跳转到main.c文件继续执行。

main.c文件如下（我把英文注释翻译了一下）：

```c
#include <inc/x86.h>
#include <inc/elf.h>

/**********************************************************************
 * 从第一个IDE硬盘引导ELF内核映像
 *
 * 磁盘布局
 *  * 这个程序是引导加载程序。它应该存储在磁盘的第一个扇区中。
 *
 *  * 第二个扇区包含内核映像。
 *
 *  * 内核映像必须是ELF格式的。
 *
 * 启动步骤
 *  * 当CPU启动时，它将BIOS加载到内存中并执行它
 *
 *  * BIOS初始化设备，设置中断例程，将启动设备的第一个扇区读入内存并跳转到它。
 *
 *  * 假设这个引导加载程序存储在硬盘驱动器的第一个扇区中，这段代码将接管…
 *
 *  * 控制在boot.s启动，设置保护模式和堆栈，让C代码运行，然后调用bootmain()
 *
 *  * 这个文件中的bootmain()接管内核，读入内核并跳转到内核
 **********************************************************************/

#define SECTSIZE	512
#define ELFHDR		((struct Elf *) 0x10000) // scratch space

void readsect(void*, uint32_t);
void readseg(uint32_t, uint32_t, uint32_t);

void
bootmain(void)
{
	struct Proghdr *ph, *eph;

	// 从磁盘读取第一页（相当于把操作系统映像文件的ELF头读入内存）
	readseg((uint32_t) ELFHDR, SECTSIZE*8, 0);

	// 判断ELF是否有效
	if (ELFHDR->e_magic != ELF_MAGIC)
		goto bad;

	// 加载程序段
	ph = (struct Proghdr *) ((uint8_t *) ELFHDR + ELFHDR->e_phoff);//表头
	eph = ph + ELFHDR->e_phnum;//表尾
	for (; ph < eph; ph++)
		//p_pa是这个段的加载地址(以及物理地址)
		readseg(ph->p_pa, ph->p_memsz, ph->p_offset);

	// 从ELF头中调用入口点
	((void (*)(void)) (ELFHDR->e_entry))();

bad:
	outw(0x8A00, 0x8A00);
	outw(0x8A00, 0x8E00);
	while (1)
		/* do nothing */;
}

//在'offset'处将'count'字节读入物理地址'pa'
void
readseg(uint32_t pa, uint32_t count, uint32_t offset)
{
	uint32_t end_pa;

	end_pa = pa + count;

	// 四舍五入到扇形边界
	pa &= ~(SECTSIZE - 1);

	// 从字节转换到扇区，内核从扇区1开始
	offset = (offset / SECTSIZE) + 1;

	// 如果这个速度太慢，我们可以一次读取很多扇区。我们写入的内存要比要求的多，但这没关系——我们按递增的顺序加载。
	while (pa < end_pa) {
		//因为我们还没有启用分页，而且我们使用的是标识段映射，所以我们可以直接使用物理地址。一旦JOS启用了MMU，情况就不一样了。
		readsect((uint8_t*) pa, offset);
		pa += SECTSIZE;
		offset++;
	}
}
```

###练习3.1

我们根据题目的要求，在0x7c00处下断点，执行，查看汇编指令：

```
(gdb)  b *0x7c00
Breakpoint 1 at 0x7c00
(gdb) c
Continuing.
[   0:7c00] => 0x7c00:	cli    

Breakpoint 1, 0x00007c00 in ?? ()
(gdb) x/30i 0x7c00
=> 0x7c00:	cli    
   0x7c01:	cld    
   0x7c02:	xor    %ax,%ax
   0x7c04:	mov    %ax,%ds
   0x7c06:	mov    %ax,%es
   0x7c08:	mov    %ax,%ss
   0x7c0a:	in     $0x64,%al
   0x7c0c:	test   $0x2,%al
   0x7c0e:	jne    0x7c0a
   0x7c10:	mov    $0xd1,%al
   0x7c12:	out    %al,$0x64
   0x7c14:	in     $0x64,%al
   0x7c16:	test   $0x2,%al
   0x7c18:	jne    0x7c14
   0x7c1a:	mov    $0xdf,%al
   0x7c1c:	out    %al,$0x60
   0x7c1e:	lgdtw  0x7c64
   0x7c23:	mov    %cr0,%eax
   0x7c26:	or     $0x1,%eax
   0x7c2a:	mov    %eax,%cr0
   0x7c2d:	ljmp   $0x8,$0x7c32
   0x7c32:	mov    $0xd88e0010,%eax
   0x7c38:	mov    %ax,%es
```

再看obj/boot/boot.asm中的代码：

```
Disassembly of section .text:

00007c00 <start>:
.set CR0_PE_ON,      0x1         # protected mode enable flag

.globl start
start:
  .code16                     # Assemble for 16-bit mode
  cli                         # Disable interrupts
    7c00:	fa                   	cli    
  cld                         # String operations increment
    7c01:	fc                   	cld    

  # Set up the important data segment registers (DS, ES, SS).
  xorw    %ax,%ax             # Segment number zero
    7c02:	31 c0                	xor    %eax,%eax
  movw    %ax,%ds             # -> Data Segment
    7c04:	8e d8                	mov    %eax,%ds
  movw    %ax,%es             # -> Extra Segment
    7c06:	8e c0                	mov    %eax,%es
  movw    %ax,%ss             # -> Stack Segment
    7c08:	8e d0                	mov    %eax,%ss

00007c0a <seta20.1>:
  # Enable A20:
  #   For backwards compatibility with the earliest PCs, physical
  #   address line 20 is tied low, so that addresses higher than
  #   1MB wrap around to zero by default.  This code undoes this.
seta20.1:
  inb     $0x64,%al               # Wait for not busy
    7c0a:	e4 64                	in     $0x64,%al
  testb   $0x2,%al
    7c0c:	a8 02                	test   $0x2,%al
  jnz     seta20.1
    7c0e:	75 fa                	jne    7c0a <seta20.1>

  movb    $0xd1,%al               # 0xd1 -> port 0x64
    7c10:	b0 d1                	mov    $0xd1,%al
  outb    %al,$0x64
    7c12:	e6 64                	out    %al,$0x64

00007c14 <seta20.2>:

seta20.2:
  inb     $0x64,%al               # Wait for not busy
    7c14:	e4 64                	in     $0x64,%al
  testb   $0x2,%al
    7c16:	a8 02                	test   $0x2,%al
  jnz     seta20.2
    7c18:	75 fa                	jne    7c14 <seta20.2>

  movb    $0xdf,%al               # 0xdf -> port 0x60
    7c1a:	b0 df                	mov    $0xdf,%al
  outb    %al,$0x60
    7c1c:	e6 60                	out    %al,$0x60
```

同上面boot.S，三个一起比较发现，在汇编代码上并没有太大出入，只是在第一段代码中，seta20.1和2被翻译为物理地址。

### 练习3.2

跟踪到boot/main中的bootmain().c，然后进入readsect()。识别与readsect()中的每个语句对应的确切的程序集指令。跟踪readsect()的其余部分，然后返回bootmain()，并标识从磁盘读取内核其余扇区的for循环的开始和结束。找出循环结束时将运行什么代码，在那里设置一个断点，然后继续运行该断点。然后逐步完成引导加载程序的其余部分。

```assembly
00007d15 <bootmain>:
{
    7d15:	55                   	push   %ebp
    7d16:	89 e5                	mov    %esp,%ebp  ;修改寄存器的值
    7d18:	56                   	push   %esi
    7d19:	53                   	push   %ebx  ;保存寄存器的值，后面要用到寄存器
	readseg((uint32_t) ELFHDR, SECTSIZE*8, 0);
    7d1a:	6a 00                	push   $0x0
    7d1c:	68 00 10 00 00       	push   $0x1000
    7d21:	68 00 00 01 00       	push   $0x10000  ;函数的三个参数压栈
    7d26:	e8 b1 ff ff ff       	call   7cdc <readseg> ;这是readseg函数的地址，我们进入到这个函数
	
00007cdc <readseg>:
{
    7cdc:	55                   	push   %ebp
    7cdd:	89 e5                	mov    %esp,%ebp
    7cdf:	57                   	push   %edi
    7ce0:	56                   	push   %esi ;保存寄存器的值
	offset = (offset / SECTSIZE) + 1;
    7ce1:	8b 7d 10             	mov    0x10(%ebp),%edi ;第三个参数
{
    7ce4:	53                   	push   %ebx
	end_pa = pa + count;
    7ce5:	8b 75 0c             	mov    0xc(%ebp),%esi  ;第二个参数
{
    7ce8:	8b 5d 08             	mov    0x8(%ebp),%ebx  ;第一个参数
	offset = (offset / SECTSIZE) + 1;
    7ceb:	c1 ef 09             	shr    $0x9,%edi  ;第三个参数逻辑右移9位，完成该功能的除法
	end_pa = pa + count;
    7cee:	01 de                	add    %ebx,%esi  ;完成加法，扇区尾部
	offset = (offset / SECTSIZE) + 1;
    7cf0:	47                   	inc    %edi  ;最后的+1
	pa &= ~(SECTSIZE - 1);
    7cf1:	81 e3 00 fe ff ff    	and    $0xfffffe00,%ebx  ;与运算，pa指向offset扇区的起始地址
	while (pa < end_pa) {
    7cf7:	39 f3                	cmp    %esi,%ebx  ;判断是否相等，即是否循环完毕
    7cf9:	73 12                	jae    7d0d <readseg+0x31>  ;while循环
		readsect((uint8_t*) pa, offset);
    7cfb:	57                   	push   %edi
    7cfc:	53                   	push   %ebx  ;压参
		offset++;
    7cfd:	47                   	inc    %edi
		pa += SECTSIZE;
    7cfe:	81 c3 00 02 00 00    	add    $0x200,%ebx
		readsect((uint8_t*) pa, offset);
    7d04:	e8 73 ff ff ff       	call   7c7c <readsect>

;进入readsecthanshu
readsect(void *dst, uint32_t offset)
{
    7c7c:	55                   	push   %ebp
    7c7d:	89 e5                	mov    %esp,%ebp
    7c7f:	57                   	push   %edi   ;保存寄存器的值
    7c80:	8b 4d 0c             	mov    0xc(%ebp),%ecx
	// wait for disk to be ready
	waitdisk();
    7c83:	e8 e2 ff ff ff       	call   7c6a <waitdisk>  ;调用该函数
}
;跳转到这里
waitdisk(void)
{
    7c6a:	55                   	push   %ebp

static inline uint8_t
inb(int port)
{
	uint8_t data;
	asm volatile("inb %w1,%0" : "=a" (data) : "d" (port));
    7c6b:	ba f7 01 00 00       	mov    $0x1f7,%edx  ;端口赋值
    7c70:	89 e5                	mov    %esp,%ebp   ;寄存器赋值
    7c72:	ec                   	in     (%dx),%al   ;in命令
	// wait for disk reaady
	while ((inb(0x1F7) & 0xC0) != 0x40)
    7c73:	83 e0 c0             	and    $0xffffffc0,%eax
    7c76:	3c 40                	cmp    $0x40,%al
    7c78:	75 f8                	jne    7c72 <waitdisk+0x8>
    		/* do nothing */;
}
    7c7a:	5d                   	pop    %ebp
    7c7b:	c3                   	ret 
;然后继续往下走，来到这个函数
static inline void
outb(int port, uint8_t data)
{
	asm volatile("outb %0,%w1" : : "a" (data), "d" (port));
    7c88:	b0 01                	mov    $0x1,%al   ;数据赋值 
    7c8a:	ba f2 01 00 00       	mov    $0x1f2,%edx  ;端口赋值
    7c8f:	ee                   	out    %al,(%dx)   ;out命令
;后面同样也是out操作
;继续往下走
// wait for disk to be ready
	waitdisk();
    7cc4:	e8 a1 ff ff ff       	call   7c6a <waitdisk>
	asm volatile("cld\n\trepne\n\tinsl"
    7cc9:	8b 7d 08             	mov    0x8(%ebp),%edi  ;第一个参数pa
    7ccc:	b9 80 00 00 00       	mov    $0x80,%ecx   ;赋值
    7cd1:	ba f0 01 00 00       	mov    $0x1f0,%edx  ;赋值
    7cd6:	fc                   	cld       ;清除方向标识
    7cd7:	f2 6d                	repnz insl (%dx),%es:(%edi) ;repnz指令 重复串操作指令，当计数器%ecx的值不为零是就一直重复后面的串操作指令。后面是insl指令，那么被重复调用的指令就是insl指令
    7cd9:	5f                   	pop    %edi
    7cda:	5d                   	pop    %ebp
    7cdb:	c3                   	ret 
    ;执行完这个函数之后，回到readseg函数
    ;循环完之后，回到bootmain，接着执行：
    if (ELFHDR->e_magic != ELF_MAGIC)
    7d2b:	83 c4 0c             	add    $0xc,%esp
    7d2e:	81 3d 00 00 01 00 7f 	cmpl   $0x464c457f,0x10000  ;验证ELF头
    7d35:	45 4c 46 
    7d38:	75 37                	jne    7d71 <bootmain+0x5c>
    ;验证完往下走：
    ph = (struct Proghdr *) ((uint8_t *) ELFHDR + ELFHDR->e_phoff);
    7d3a:	a1 1c 00 01 00       	mov    0x1001c,%eax  ;这里应该是放了一个偏移
	eph = ph + ELFHDR->e_phnum;
    7d3f:	0f b7 35 2c 00 01 00 	movzwl 0x1002c,%esi   ;又是一个偏移吧
	ph = (struct Proghdr *) ((uint8_t *) ELFHDR + ELFHDR->e_phoff);
    7d46:	8d 98 00 00 01 00    	lea    0x10000(%eax),%ebx  ;得到ph即起始物理地址
	eph = ph + ELFHDR->e_phnum;
    7d4c:	c1 e6 05             	shl    $0x5,%esi  ;左移五位，求大小
    7d4f:	01 de                	add    %ebx,%esi  ;得到结束的地址
    ;接着是几个for循环
    for (; ph < eph; ph++)  ;这个循环把内核的每一段放到内存
    7d51:	39 f3                	cmp    %esi,%ebx
    7d53:	73 16                	jae    7d6b <bootmain+0x56>
		readseg(ph->p_pa, ph->p_memsz, ph->p_offset);
    7d55:	ff 73 04             	pushl  0x4(%ebx)
    7d58:	ff 73 14             	pushl  0x14(%ebx)  ;参数压栈
	for (; ph < eph; ph++)
    7d5b:	83 c3 20             	add    $0x20,%ebx  ;指向下一个
		readseg(ph->p_pa, ph->p_memsz, ph->p_offset);
    7d5e:	ff 73 ec             	pushl  -0x14(%ebx)
    7d61:	e8 76 ff ff ff       	call   7cdc <readseg>
	for (; ph < eph; ph++)
    7d66:	83 c4 0c             	add    $0xc,%esp
    7d69:	eb e6                	jmp    7d51 <bootmain+0x3c>  ;循环
	((void (*)(void)) (ELFHDR->e_entry))();
    7d6b:	ff 15 18 00 01 00    	call   *0x10018  ;循环结束后，把控制权交给操作系统内核
}
```

**ELF头结构**

```
typedef struct
{
  unsigned char	e_ident[EI_NIDENT];	/* Magic number和其它信息 */
  Elf32_Half	e_type;			/* Object file type */
  Elf32_Half	e_machine;		/* Architecture */
  Elf32_Word	e_version;		/* Object file version */
  Elf32_Addr	e_entry;		/* Entry point virtual address */
  Elf32_Off	e_phoff;		/* Program header table file offset */
  Elf32_Off	e_shoff;		/* Section header table file offset */
  Elf32_Word	e_flags;		/* Processor-specific flags */
  Elf32_Half	e_ehsize;		/* ELF header size in bytes */
  Elf32_Half	e_phentsize;		/* Program header table entry size */
  Elf32_Half	e_phnum;		/* Program header table entry count */
  Elf32_Half	e_shentsize;		/* Section header table entry size */
  Elf32_Half	e_shnum;		/* Section header table entry count */
  Elf32_Half	e_shstrndx;		/* Section header string table index */
} Elf32_Ehdr;
```



### 回答问题



1. 处理器在什么时候开始执行32位代码?究竟是什么导致了从16位模式到32位模式的转换?

   计算机最开始是运行于实模式的，此时是16bit，在boot.s中有一句`ljmp $PROT_MODE_CSEG, $protcseg`将工作模式修改为保护模式，此时位于32位工作模式。

2. boot loader执行的最后一条指令是什么?它刚刚加载的内核的第一条指令是什么?

   最后一条指令是((void (*)(void)) (ELFHDR->e_entry))()，跳转到操作系统内核程序的起始指令处。

   内核的第一条指令是movw $0x1234, 0x472。

3. 内核的第一条指令在哪里?

   位于/kern/entry.S文件中。

4. boot loader如何决定为了从磁盘获取整个内核必须读取多少扇区?它从哪里找到这些信息?

   Program Header Table中有操作系统有关段、扇区的信息。这个表中的每个表项分别对应操作系统的一个段。并且每个表项的内容包括这个段的大小，段起始地址偏移等等信息。所以我们可以通过表项所提供的信息来确定内核占用多少个扇区。

   这个表存放在操作系统内核映像文件的ELF头部信息中。