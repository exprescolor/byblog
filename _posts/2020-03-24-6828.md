---
layout:     post   				    # 使用的布局（不需要改）
title:      828-lab1-part1-环境配置		# 标题 
#subtitle:   脚本，xss #副标题
date:       2020-03-24 				# 时间
author:     s-seven 						# 作者
header-img: img/post-bg-2015.jpg 	#这篇文章标题背景图片
catalog: true 						# 是否归档
tags:								#标签
    - 基础知识
---

今天打开了第三阶段的一个实验，是2018年MIT 6.828 JOS内核实验，去官网转了一圈，一脸懵逼。

官网大概说了一下需要的工具，项目的课程表、代码，和一些这门课的学习资料，那就从环境的配置开始吧。

# 平台工具

> https://pdos.csail.mit.edu/6.828/2018/tools.html  (平台搭建的指导页面)

## 测试编译器工具链

“编译器工具链”是一组程序，包括C编译器、汇编器和链接器，它们将代码转换成可执行的二进制文件。我们需要需要一个为ELF二进制格式的32位Intel体系结构(“x86”体系结构)生成代码的编译器工具链。

两句代码来测试我们的Linux环境

`objdump -i`

`gcc -m32 -print-libgcc-file-name`

![image-20200324155459331.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5dzj5yqyj30fx04caam.jpg)



![image-20200324155622078.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5dzzq2kdj30ln01e74d.jpg)



如果这两个命令都执行成功的话，进入到下一步

**大端序和小端序**

即字节的存储顺序，如果数据都是单字节的，那怎么存储无所谓了，但是对于多字节数据，比如int，double等，就要考虑存储的顺序了。注意字节序是硬件层面的东西，对于软件来说通常是透明的。再说白一点，字节序通常只和你使用的处理器架构有关，而和编程语言无关，比如常见的Intel x86系列就是小端序。

大端序即数据的高位字节存放在地址的低端 低位字节存放在地址高端。

小端序即数据的高位字节存放在地址的高端 低位字节存放在地址低端。

举例：

var = 0x11223344，对于这个变量的最高字节为0x11，最低字节为0x44

大端模式存储（存储地址为16位）

地址                    数据

0x0004(高地址)     0x44

0x0003                 0x33

0x0002                 0x22

0x0001(低地址)     0x11

小端模式存储（存储地址为16位）

地址                    数据

0x0004(高地址)     0x11

0x0003                 0x22

0x0002                 0x33

0x0001(低地址)     0x44

# qemu安装

QEMU是一个现代和快速的PC仿真器。

首先下载qemu：

`git clone https://github.com/mit-pdos/6.828-qemu.git qemu	`

![image-20200324164845285.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e0ceny2j30ol02z74v.jpg)

这个还挺快的，几分钟就下下来了

进入qemu目录下：

`cd qemu`

然后输入命令：

`./configure`

这一步看似简单，我报错到崩溃，首先遇到了这个错误：

![image-20200324175439222.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e0jmko2j30gu03ddg7.jpg)

本着少什么就加什么的原则，我一共添加了：

```
sudo apt-get install pkg-config
sudo apt-get install libfdt
sudo apt-get install libfdt
sudo apt-get install libfdt-dev
```

最后成功之后显示一些配置信息

我们执行一下make指令：

![image-20200324175601810.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e0rmeb3j30gy049dgf.jpg)



哦呵呵呵我又开始报错了，下面这个错误是缺少了一个头文件

![image-20200324175628785.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e0yfz99j30oj0ddtai.jpg)

解决方法：
在 qga/commands-posix.c文件中加 #include <sys/sysmacros.h>

然后继续执行make，出现：

![image-20200324175648936.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e14q7x4j30oq0ag0v4.jpg)

解决方法：config-host.mak文件中的-Werror去掉

继续make：

![image-20200324175725048.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e1azogtj30d60343ys.jpg)

以上是make执行完后最后几条信息，我们再执行最后一步

`make install`

![image-20200324175757923.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e1iluofj30hk02zgm2.jpg)

这一步没出现什么问题

Git一下源码,这一步将源码放在lab目录下

`git clone https://pdos.csail.mit.edu/6.828/2018/jos.git lab`

make一下，出现一下错误，解决方法是安装gcc-multilib

![image-20200324180712673.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e1o5ucgj30lt04z0tz.jpg)

最后一步make qemu

![image-20200324180811406.png](http://ww1.sinaimg.cn/large/005KQQDely1gd5e1toj60j30or0cudi0.jpg)

出现这个样子就安装成功啦

bug体质的我每逢安装必报错，好在每个error都能在网上找到解决方案，最后也算是把环境配好了。明天开始学习这个项目，根据网站给出的汇编参考资料，操作系统参考书和上课的算是课件吧，一步一步来，总会好的。

# Part 1:_PC Bootstrap

## 开始学习x86汇编语言

这里给了一本参考资料介绍了汇编语言，可以参照这本书来查找汇编指令什么的，

**练习1**

熟悉6.828参考页上提供的汇编语言材料。你现在不必阅读它们，但是在读写x86汇编时，可以参考其中的一些内容。

好像并没有什么实质性的内容，但是要有一些汇编基础才行。

## 仿真x86

这里就是qemu的配置，昨天已经配置好了，遇到的问题和解决方案课件里也说的很清楚。

## PC的物理地址空间

pc的物理地址空间分布长这样：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd5zcnrsqkj30cp0fbgm7.jpg)

早期计算机的处理器并不能存储这么多物理内存，是从16kb,32kb,64kb发展起来的。

从0x000A0000到0x000FFFFF的384KB区域由硬件保留，用于特殊用途，比如视频显示缓冲区和非易失性内存中的固件。这个保留区域中最重要的部分是基本输入/输出系统(BIOS)，它占据从0x000F0000到0x000FFFFF的64KB区域。在早期的pc机中，BIOS被保存在真正的只读存储器(ROM)中，但是现在的pc机将BIOS保存在可更新的闪存中。BIOS负责执行基本的系统初始化，如激活显卡和检查安装的内存量。在执行这个初始化之后，BIOS从适当的位置(如软盘、硬盘、CD-ROM或网络)加载操作系统，并将机器的控制权传递给操作系统。

当Intel最终使用80286和80386处理器“打破了1m的限制”，这两个处理器分别支持16MB和4GB的物理地址空间时，PC架构师仍然保留了低1MB物理地址空间的原始布局，以确保与现有软件的向后兼容性。因此，现代pc机在物理内存从0x000A0000到0x00100000之间有一个“洞”，将RAM划分为“低”或“传统内存”(前640KB)和“扩展内存”(其他所有内存)。此外，PC的32位物理地址空间(首先是物理RAM)的最顶层的一些空间现在通常由BIOS保留，供32位PCI设备使用。

最近的x86处理器可以支持超过4GB的物理RAM，因此RAM可以进一步扩展到0xFFFFFFFF之上。在这种情况下，BIOS必须在32位可寻址区域的顶部的系统RAM中留下第二个洞，为这些32位设备的映射留下空间。由于设计上的限制，JOS将只使用PC的前256MB物理内存，所以现在我们将假设所有PC都“只有”32位物理地址空间。但是，处理复杂的物理地址空间和经过多年发展的硬件组织的其他方面是操作系统开发的重要实践挑战之一。

## The ROM BIOS

这一部分我们利用qemu和gdb来实现对操作系统的debug

首先打开两个终端，进入lab所在目录，其中一个终端输入make qemu-gdb,

![11.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd6eth3tbrj30on060aak.jpg)

另一个终端输入gdb，

![12.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd6etnq9wgj30t90h7tc5.jpg)

这里出现了一个错误（好像是两个），解决方案也上面也说的很清楚，往/home/s-seven/.gdbinit文件里添加两行代码

于是

`vi /home/s-seven/.gdbinit`

进入文件，添加

`add-auto-load-safe-path /home/s-seven/lab/.gdbinit
set auto-load safe-path /`

这两行到文件中，回到lab目录下面重新gdb，OK了，出现

![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd6favygz5j30oo06l3ze.jpg)

然后我们看到`[f000:fff0]    0xffff0:	ljmp   $0xf000,$0xe05b`这一句，就是要执行的第一个指令的反汇编。通过这些输出信息我们可以总结：

·IBM PC在物理地址0x000ffff0处开始执行，它位于为ROM BIOS保留的64KB区域的最顶端。

·PC从CS = 0xf000和IP = 0xfff0开始执行。

·第一个要执行的指令是一条jmp指令，它跳转到分段的地址CS = 0xf000和IP = 0xe05b。

qemu这样开始与英特尔设计8088处理器的方式有关，PC中，BIOS的物理地址范围是 0x000f0000-0x000fffff，在处理器复位时，(模拟的)处理器进入实模式，将CS设置为0xf000，将IP设置为0xfff0，这样执行就从那个(CS:IP)段地址开始。

**实模式**

PC机启动时，CPU运行在实模式下，进入操作系统内核后，运行在保护模式下。实模式是早期CPU的工作模式，现代处理器在启动时运行于实模式，在启动完成后运行于保护模式。BIOS就运行在实模式下。

在实模式下(PC机启动的模式)，地址转换按照公式进行:物理地址= 16 *段地址+偏移量。因此，当PC将CS设为0xf000，将IP设为0xfff0时，所引用的物理地址为:

16 * 0xf000 + 0xfff0   #16进制乘法
   = 0xf0000 + 0xfff0     # 进位加0
   = 0xffff0 

##练习2

使用GDB的si(步骤指令)命令跟踪ROM BIOS以获得更多的指令，并尝试猜测它可能在做什么。您可能想要查看Phil Storrs I/O端口描述，以及6.828参考资料页面上的其他资料。不需要弄清楚所有的细节-只是BIOS首先要做的事情的一般概念。

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd6gydz9zpj30fy0993z8.jpg)

查一下si指令，汇编指令的step into，于是我们可以直接输入si命令进行跟踪。

​	`0xffff0:	ljmp   $0xf000,$0xe05b`

先看第一行命令。这是一条跳转指令，跳转到0xe05b处。

`0xfe05b:	cmpl   $0x0,%cs:0x6ac8`

第二条，比较0x0和%cs:0x6ac8处的值比较，其中%cs:0x6ac8这个格式，就是 段地址：段内偏移 的格式，可以根据计算公式得到物理地址。

`0xfe062:	jne    0xfd2e1`

第三条，如果比较值不相等，则跳转至0xfd2e1地址处。

`0xfe066:	xor    %dx,%dx`

第四条，根据这条指令的地址0xfe066得到，上一条指令并没有跳转，于是这一步将dx寄存器清零。

`0xfe068:	mov    %dx,%ss`

第五条，这个样子ss寄存器也被清零了。

`0xfe06a:	mov    $0x7000,%esp`

第六条，将寄存器esp赋值为0x7000。

`0xfe070:	mov    $0xf34c2,%edx`

第七条，将edx寄存器赋值为0xf34c2。

`0xfe076:	jmp    0xfd15c`

第八条，跳转至0xfd15c

`0xfd15c:	mov    %eax,%ecx`

这一条，将eax寄存器的值赋给ecx

`0xfd15f:	cli`

这一条指令，我查了一下，是关闭中断指令，避免在启动时发生中断。

`0xfd160:	cld `

清除方向标志，即将标志位置0，在字串操作中使变址寄存器SI或DI的地址指针自动增加，字串处理由前往后。

相反功能指令是STD，将方向标志位DF置1，在字串操作中使SI或DI的地址指针自动递减，字串处理由后往前。

`0xfd161:	mov    $0x8f,%eax`

给eax寄存器赋值0x8f

`0xfd167:	out    %al,$0x70`

**out指令** 把寄存器al的值向端口写入，这里端口是0x70

`0xfd169:	in     $0x71,%al`

**in指令**：读取端口的值

**0x70和0x71** ：0x70端口和0x71端口是用于控制系统中一个叫做CMOS的设备，这个设备是一个低功耗的存储设备，它可以用于在计算机关闭时存储一些信息。其中0x70可以叫做索引寄存器，这个8位寄存器的最高位是不可屏蔽中断(NMI)使能位。如果你把这个位置1，则NMI不会被响应。低7位用于指定CMOS存储器中的存储单元地址。

在向0x70端口写入值之后，必须对0x71端口有操作，否则RTC就会处在an unknown state之中。

**CMOS**：CMOS中可以控制跟PC相关的多个功能，其中最重要的就是时钟设备（Real Time Clock）的 ，它还可以控制是否响应不可屏蔽中断NMI(Non-Maskable Interrupt)。

所以这三条指令是用来关闭了NMI中断

`0xfd16b:	in     $0x92,%al`

`0xfd16d:	or     $0x2,%al`

`0xfd16f:	out    %al,$0x92`

这三条操作的是0x92这个端口，我们查到这个端口控制的是 PS/2系统控制端口A，然后和0x2或运算，再把值读入寄存器。（这是在干什么呢，我没搞懂。。）

`0xfd171:	lidtw  %cs:0x6ab8`

lidt指令：加载中断向量表寄存器(IDTR)。这个指令会把从地址0xf6ab8起始的后面6个字节的数据读入到中断向量表寄存器(IDTR)中。中断是操作系统中非常重要的一部分，有了中断操作系统才能真正实现进程。每一种中断都有自己对应的中断处理程序，那么这个中断的处理程序的首地址就叫做这个中断的中断向量。中断向量表自然是存放所有中断向量的表了。

`0xfd177:	lgdtw  %cs:0x6a74`

把从0xf6a74为起始地址处的6个字节的值加载到全局描述符表格寄存器中GDTR中。

```
0xfd17d:	mov    %cr0,%eax
0xfd180:	or     $0x1,%eax
0xfd184:	mov    %eax,%cr0
```

这三句的操作把CR0寄存器的最低位(0bit)置1。CR0的位0是启用保护（Protection Enable）标志。当设置该位时即开启了保护模式；当复位时即进入实地址模式。

**控制寄存器**：控制寄存器（CR0～CR3）用于控制和确定处理器的操作模式以及当前执行任务的特性，如图4-3所示。CR0中含有控制处理器操作模式和状态的系统控制标志；CR1保留不用；CR2含有导致页错误的线性地址；CR3中含有页目录表物理内存基地址，因此该寄存器也被称为页目录基地址寄存器PDBR（Page-Directory Base address Register）。>https://blog.csdn.net/echoisland/article/details/6726730 (这篇文章讲了这几个寄存器)

```
0xfd187:   ljmpl  $0x8, $0xfd18f
0xfd18f:   mov  $0x10, %eax
0xfd194:   mov  %eax, %ds
0xfd196:   mov  %eax, %es
0xfd198:   mov  %eax, %ss
0xfd19a:   mov  %eax, %fs
0xfd19c:   mov  %eax, %gs
```

这几句修改了寄存器的值，查资料得，加载完GDTR寄存器之后，要重新加载段寄存器的值。

以上，只是简单的了解了这些汇编指令都是什么，至于干了什么以及为什么要这么做，我现在还不知道。。。



当BIOS运行时，它设置一个中断描述符表，并初始化各种设备，如VGA显示器。这就是我们在QEMU窗口中看到的“Starting SeaBIOS”消息的来源。

在初始化PCI总线和BIOS知道的所有重要设备之后，它将搜索可启动的设备，如软盘、硬盘或CD-ROM。最后，当它找到一个可引导的磁盘时，BIOS从磁盘读取引导加载程序，并将控制传输给它。

**BIOS**

是什么：基本输入输出系统

干什么：作为PC启动后运行的一个程序，把操作系统从磁盘中导入内存，再把控制权转交给操作系统。

#Part 2: The Boot Loader

**扇区**：个人计算机的软盘和硬盘被划分为512字节的区域，称为扇区。扇区是磁盘的最小传输粒度:每个读或写操作的大小必须是一个或多个扇区，并且在扇区边界上对齐。如果磁盘是可引导的，则第一个扇区称为引导扇区，因为这是引导加载程序代码所在的位置。

当BIOS找到可引导的软盘或硬盘时，它将512字节的引导扇区装入物理地址0x7c00到0x7dff的内存中，然后使用jmp指令将CS:IP设置为0000:7c00，将控制权交给引导加载程序。与BIOS加载地址一样，这些地址是相当随意的—但是它们是固定的，并且对于pc来说是标准化的。

从CD-ROM引导的能力在PC的发展过程中出现得很晚，因此PC架构师抓住机会重新思考了一下引导过程。因此，现代BIOS从CD-ROM引导的方式有点复杂(也更强大)。cd - rom使用2048字节而不是512字节的扇区大小，BIOS可以在将控制转移到内存之前将更大的引导映像从磁盘加载到内存(而不仅仅是一个扇区)。

然而，对于6.828，我们将使用传统的硬盘驱动器引导机制，这意味着我们的引导加载程序必须小于512字节。boot loader由一个汇编语言源文件(boot/boot.s)和一个C源文件(boot/main.c)组成。

boot loader必须执行两个主要功能：

1. boot loader将处理器从实际模式切换到32位保护模式，因为只有在这种模式下，软件才能访问处理器物理地址空间中所有大于1MB的内存。转换偏移量之后是32位而不是16位。

2. boot loader通过x86的特殊I/O指令直接访问IDE磁盘设备寄存器，从而从硬盘读取内核。

理解boot loader源代码之后，查看文件obj/boot/boot.asm。这个文件是我们的GNUmakefile在编译引导加载程序之后创建的引导加载程序的一个反汇编。这个反汇编文件可以很容易地查看物理内存中所有引导加载程序的代码所在的位置，并且可以更容易地跟踪在GDB中单步执行引导加载程序时发生的情况。

## 练习3

> 请参阅实验室工具指南，特别是关于GDB命令的部分。熟悉GDB，这也包括一些对操作系统工作有用的深奥的GDB命令。
>
> 在地址0x7c00处设置一个断点，引导扇区将在这里加载。继续执行，直到该断点。跟踪boot/boot.s中的代码。使用源代码和反汇编文件obj/boot/boot.asm跟踪您的位置。还可以使用GDB中的x/i命令在引导加载程序中对指令序列进行反汇编，并将原始的引导加载程序源代码与obj/boot/boot.asm中的反汇编进行比较和GDB。
>
> 跟踪到boot/main中的bootmain().c，然后进入readsect()。识别与readsect()中的每个语句对应的确切的程序集指令。跟踪readsect()的其余部分，然后返回bootmain()，并标识从磁盘读取内核其余扇区的for循环的开始和结束。找出循环结束时将运行什么代码，在那里设置一个断点，然后继续运行该断点。然后逐步完成引导加载程序的其余部分。

###boot loader的组成

先看一下boot.S文件：

```assembly
#include <inc/mmu.h>

# Start the CPU: switch to 32-bit protected mode, jump into C.
# The BIOS loads this code from the first sector of the hard disk into
# memory at physical address 0x7c00 and starts executing in real mode
# with %cs=0 %ip=7c00.

.set PROT_MODE_CSEG, 0x8         # kernel code segment selector
.set PROT_MODE_DSEG, 0x10        # kernel data segment selector
.set CR0_PE_ON,      0x1         # protected mode enable flag

.globl start
start:
  .code16                     # Assemble for 16-bit mode
  cli                         # Disable interrupts
  cld                         # String operations increment

  # Set up the important data segment registers (DS, ES, SS).
  xorw    %ax,%ax             # Segment number zero
  movw    %ax,%ds             # -> Data Segment
  movw    %ax,%es             # -> Extra Segment
  movw    %ax,%ss             # -> Stack Segment

  # Enable A20:
  #   For backwards compatibility with the earliest PCs, physical
  #   address line 20 is tied low, so that addresses higher than
  #   1MB wrap around to zero by default.  This code undoes this.
seta20.1:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.1

  movb    $0xd1,%al               # 0xd1 -> port 0x64
  outb    %al,$0x64
;D1指令代表下一次写入0x60端口的数据将被写入给804x控制器的输出端口。可以理解为下一个写入0x60端口的数据是一个控制指令。
seta20.2:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.2

  movb    $0xdf,%al               # 0xdf -> port 0x60
  outb    %al,$0x60
;DF的含义可以从图中看到，使能A20线，代表可以进入保护模式了。

  # Switch from real to protected mode, using a bootstrap GDT从实际模式切换到保护模式
  # and segment translation that makes virtual addresses 
  # identical to their physical addresses, so that the 
  # effective memory map does not change during the switch.
  lgdt    gdtdesc
  movl    %cr0, %eax
  orl     $CR0_PE_ON, %eax
  movl    %eax, %cr0
  
  # Jump to next instruction, but in 32-bit code segment.
  # Switches processor into 32-bit mode.
  ljmp    $PROT_MODE_CSEG, $protcseg

  .code32                     # Assemble for 32-bit mode
protcseg:
  # Set up the protected-mode data segment registers
  movw    $PROT_MODE_DSEG, %ax    # Our data segment selector
  movw    %ax, %ds                # -> DS: Data Segment
  movw    %ax, %es                # -> ES: Extra Segment
  movw    %ax, %fs                # -> FS
  movw    %ax, %gs                # -> GS
  movw    %ax, %ss                # -> SS: Stack Segment
  
  # Set up the stack pointer and call into C.
  movl    $start, %esp
  call bootmain

  # If bootmain returns (it shouldn't), loop.
spin:
  jmp spin

# Bootstrap GDT
.p2align 2                                # force 4 byte alignment
gdt:
  SEG_NULL				# null seg
  SEG(STA_X|STA_R, 0x0, 0xffffffff)	# code seg
  SEG(STA_W, 0x0, 0xffffffff)	        # data seg
;xv6并没有使用分段机制，也就是说数据和代码都是写在一起的，所以数据段和代码段的起始地址都是0x0，大小都是0xffffffff=4GB。
gdtdesc:
  .word   0x17                            # sizeof(gdt) - 1
  .long   gdt                             # address gdt
```

上面的代码进行了一些关闭中断，寄存器的初始化，保护模式，端口的设置等一些操作，这些操作结束后，会根据call bootmain跳转到main.c文件继续执行。

main.c文件如下（我把英文注释翻译了一下）：

```c
#include <inc/x86.h>
#include <inc/elf.h>

/**********************************************************************
 * 从第一个IDE硬盘引导ELF内核映像
 *
 * 磁盘布局
 *  * 这个程序是引导加载程序。它应该存储在磁盘的第一个扇区中。
 *
 *  * 第二个扇区包含内核映像。
 *
 *  * 内核映像必须是ELF格式的。
 *
 * 启动步骤
 *  * 当CPU启动时，它将BIOS加载到内存中并执行它
 *
 *  * BIOS初始化设备，设置中断例程，将启动设备的第一个扇区读入内存并跳转到它。
 *
 *  * 假设这个引导加载程序存储在硬盘驱动器的第一个扇区中，这段代码将接管…
 *
 *  * 控制在boot.s启动，设置保护模式和堆栈，让C代码运行，然后调用bootmain()
 *
 *  * 这个文件中的bootmain()接管内核，读入内核并跳转到内核
 **********************************************************************/

#define SECTSIZE	512
#define ELFHDR		((struct Elf *) 0x10000) // scratch space

void readsect(void*, uint32_t);
void readseg(uint32_t, uint32_t, uint32_t);

void
bootmain(void)
{
	struct Proghdr *ph, *eph;

	// 从磁盘读取第一页（相当于把操作系统映像文件的ELF头读入内存）
	readseg((uint32_t) ELFHDR, SECTSIZE*8, 0);

	// 判断ELF是否有效
	if (ELFHDR->e_magic != ELF_MAGIC)
		goto bad;

	// 加载程序段
	ph = (struct Proghdr *) ((uint8_t *) ELFHDR + ELFHDR->e_phoff);//表头
	eph = ph + ELFHDR->e_phnum;//表尾
	for (; ph < eph; ph++)
		//p_pa是这个段的加载地址(以及物理地址)
		readseg(ph->p_pa, ph->p_memsz, ph->p_offset);

	// 从ELF头中调用入口点
	((void (*)(void)) (ELFHDR->e_entry))();

bad:
	outw(0x8A00, 0x8A00);
	outw(0x8A00, 0x8E00);
	while (1)
		/* do nothing */;
}

//在'offset'处将'count'字节读入物理地址'pa'
void
readseg(uint32_t pa, uint32_t count, uint32_t offset)
{
	uint32_t end_pa;

	end_pa = pa + count;

	// 四舍五入到扇形边界
	pa &= ~(SECTSIZE - 1);

	// 从字节转换到扇区，内核从扇区1开始
	offset = (offset / SECTSIZE) + 1;

	// 如果这个速度太慢，我们可以一次读取很多扇区。我们写入的内存要比要求的多，但这没关系——我们按递增的顺序加载。
	while (pa < end_pa) {
		//因为我们还没有启用分页，而且我们使用的是标识段映射，所以我们可以直接使用物理地址。一旦JOS启用了MMU，情况就不一样了。
		readsect((uint8_t*) pa, offset);
		pa += SECTSIZE;
		offset++;
	}
}
```

###练习3.1

我们根据题目的要求，在0x7c00处下断点，执行，查看汇编指令：

```
(gdb)  b *0x7c00
Breakpoint 1 at 0x7c00
(gdb) c
Continuing.
[   0:7c00] => 0x7c00:	cli    

Breakpoint 1, 0x00007c00 in ?? ()
(gdb) x/30i 0x7c00
=> 0x7c00:	cli    
   0x7c01:	cld    
   0x7c02:	xor    %ax,%ax
   0x7c04:	mov    %ax,%ds
   0x7c06:	mov    %ax,%es
   0x7c08:	mov    %ax,%ss
   0x7c0a:	in     $0x64,%al
   0x7c0c:	test   $0x2,%al
   0x7c0e:	jne    0x7c0a
   0x7c10:	mov    $0xd1,%al
   0x7c12:	out    %al,$0x64
   0x7c14:	in     $0x64,%al
   0x7c16:	test   $0x2,%al
   0x7c18:	jne    0x7c14
   0x7c1a:	mov    $0xdf,%al
   0x7c1c:	out    %al,$0x60
   0x7c1e:	lgdtw  0x7c64
   0x7c23:	mov    %cr0,%eax
   0x7c26:	or     $0x1,%eax
   0x7c2a:	mov    %eax,%cr0
   0x7c2d:	ljmp   $0x8,$0x7c32
   0x7c32:	mov    $0xd88e0010,%eax
   0x7c38:	mov    %ax,%es
```

再看obj/boot/boot.asm中的代码：

```
Disassembly of section .text:

00007c00 <start>:
.set CR0_PE_ON,      0x1         # protected mode enable flag

.globl start
start:
  .code16                     # Assemble for 16-bit mode
  cli                         # Disable interrupts
    7c00:	fa                   	cli    
  cld                         # String operations increment
    7c01:	fc                   	cld    

  # Set up the important data segment registers (DS, ES, SS).
  xorw    %ax,%ax             # Segment number zero
    7c02:	31 c0                	xor    %eax,%eax
  movw    %ax,%ds             # -> Data Segment
    7c04:	8e d8                	mov    %eax,%ds
  movw    %ax,%es             # -> Extra Segment
    7c06:	8e c0                	mov    %eax,%es
  movw    %ax,%ss             # -> Stack Segment
    7c08:	8e d0                	mov    %eax,%ss

00007c0a <seta20.1>:
  # Enable A20:
  #   For backwards compatibility with the earliest PCs, physical
  #   address line 20 is tied low, so that addresses higher than
  #   1MB wrap around to zero by default.  This code undoes this.
seta20.1:
  inb     $0x64,%al               # Wait for not busy
    7c0a:	e4 64                	in     $0x64,%al
  testb   $0x2,%al
    7c0c:	a8 02                	test   $0x2,%al
  jnz     seta20.1
    7c0e:	75 fa                	jne    7c0a <seta20.1>

  movb    $0xd1,%al               # 0xd1 -> port 0x64
    7c10:	b0 d1                	mov    $0xd1,%al
  outb    %al,$0x64
    7c12:	e6 64                	out    %al,$0x64

00007c14 <seta20.2>:

seta20.2:
  inb     $0x64,%al               # Wait for not busy
    7c14:	e4 64                	in     $0x64,%al
  testb   $0x2,%al
    7c16:	a8 02                	test   $0x2,%al
  jnz     seta20.2
    7c18:	75 fa                	jne    7c14 <seta20.2>

  movb    $0xdf,%al               # 0xdf -> port 0x60
    7c1a:	b0 df                	mov    $0xdf,%al
  outb    %al,$0x60
    7c1c:	e6 60                	out    %al,$0x60
```

同上面boot.S，三个一起比较发现，在汇编代码上并没有太大出入，只是在第一段代码中，seta20.1和2被翻译为物理地址。

### 练习3.2

跟踪到boot/main中的bootmain().c，然后进入readsect()。识别与readsect()中的每个语句对应的确切的程序集指令。跟踪readsect()的其余部分，然后返回bootmain()，并标识从磁盘读取内核其余扇区的for循环的开始和结束。找出循环结束时将运行什么代码，在那里设置一个断点，然后继续运行该断点。然后逐步完成引导加载程序的其余部分。

```assembly
00007d15 <bootmain>:
{
    7d15:	55                   	push   %ebp
    7d16:	89 e5                	mov    %esp,%ebp  ;修改寄存器的值
    7d18:	56                   	push   %esi
    7d19:	53                   	push   %ebx  ;保存寄存器的值，后面要用到寄存器
	readseg((uint32_t) ELFHDR, SECTSIZE*8, 0);
    7d1a:	6a 00                	push   $0x0
    7d1c:	68 00 10 00 00       	push   $0x1000
    7d21:	68 00 00 01 00       	push   $0x10000  ;函数的三个参数压栈
    7d26:	e8 b1 ff ff ff       	call   7cdc <readseg> ;这是readseg函数的地址，我们进入到这个函数
	
00007cdc <readseg>:
{
    7cdc:	55                   	push   %ebp
    7cdd:	89 e5                	mov    %esp,%ebp
    7cdf:	57                   	push   %edi
    7ce0:	56                   	push   %esi ;保存寄存器的值
	offset = (offset / SECTSIZE) + 1;
    7ce1:	8b 7d 10             	mov    0x10(%ebp),%edi ;第三个参数
{
    7ce4:	53                   	push   %ebx
	end_pa = pa + count;
    7ce5:	8b 75 0c             	mov    0xc(%ebp),%esi  ;第二个参数
{
    7ce8:	8b 5d 08             	mov    0x8(%ebp),%ebx  ;第一个参数
	offset = (offset / SECTSIZE) + 1;
    7ceb:	c1 ef 09             	shr    $0x9,%edi  ;第三个参数逻辑右移9位，完成该功能的除法
	end_pa = pa + count;
    7cee:	01 de                	add    %ebx,%esi  ;完成加法，扇区尾部
	offset = (offset / SECTSIZE) + 1;
    7cf0:	47                   	inc    %edi  ;最后的+1
	pa &= ~(SECTSIZE - 1);
    7cf1:	81 e3 00 fe ff ff    	and    $0xfffffe00,%ebx  ;与运算，pa指向offset扇区的起始地址
	while (pa < end_pa) {
    7cf7:	39 f3                	cmp    %esi,%ebx  ;判断是否相等，即是否循环完毕
    7cf9:	73 12                	jae    7d0d <readseg+0x31>  ;while循环
		readsect((uint8_t*) pa, offset);
    7cfb:	57                   	push   %edi
    7cfc:	53                   	push   %ebx  ;压参
		offset++;
    7cfd:	47                   	inc    %edi
		pa += SECTSIZE;
    7cfe:	81 c3 00 02 00 00    	add    $0x200,%ebx
		readsect((uint8_t*) pa, offset);
    7d04:	e8 73 ff ff ff       	call   7c7c <readsect>

;进入readsecthanshu
readsect(void *dst, uint32_t offset)
{
    7c7c:	55                   	push   %ebp
    7c7d:	89 e5                	mov    %esp,%ebp
    7c7f:	57                   	push   %edi   ;保存寄存器的值
    7c80:	8b 4d 0c             	mov    0xc(%ebp),%ecx
	// wait for disk to be ready
	waitdisk();
    7c83:	e8 e2 ff ff ff       	call   7c6a <waitdisk>  ;调用该函数
}
;跳转到这里
waitdisk(void)
{
    7c6a:	55                   	push   %ebp

static inline uint8_t
inb(int port)
{
	uint8_t data;
	asm volatile("inb %w1,%0" : "=a" (data) : "d" (port));
    7c6b:	ba f7 01 00 00       	mov    $0x1f7,%edx  ;端口赋值
    7c70:	89 e5                	mov    %esp,%ebp   ;寄存器赋值
    7c72:	ec                   	in     (%dx),%al   ;in命令
	// wait for disk reaady
	while ((inb(0x1F7) & 0xC0) != 0x40)
    7c73:	83 e0 c0             	and    $0xffffffc0,%eax
    7c76:	3c 40                	cmp    $0x40,%al
    7c78:	75 f8                	jne    7c72 <waitdisk+0x8>
    		/* do nothing */;
}
    7c7a:	5d                   	pop    %ebp
    7c7b:	c3                   	ret 
;然后继续往下走，来到这个函数
static inline void
outb(int port, uint8_t data)
{
	asm volatile("outb %0,%w1" : : "a" (data), "d" (port));
    7c88:	b0 01                	mov    $0x1,%al   ;数据赋值 
    7c8a:	ba f2 01 00 00       	mov    $0x1f2,%edx  ;端口赋值
    7c8f:	ee                   	out    %al,(%dx)   ;out命令
;后面同样也是out操作
;继续往下走
// wait for disk to be ready
	waitdisk();
    7cc4:	e8 a1 ff ff ff       	call   7c6a <waitdisk>
	asm volatile("cld\n\trepne\n\tinsl"
    7cc9:	8b 7d 08             	mov    0x8(%ebp),%edi  ;第一个参数pa
    7ccc:	b9 80 00 00 00       	mov    $0x80,%ecx   ;赋值
    7cd1:	ba f0 01 00 00       	mov    $0x1f0,%edx  ;赋值
    7cd6:	fc                   	cld       ;清除方向标识
    7cd7:	f2 6d                	repnz insl (%dx),%es:(%edi) ;repnz指令 重复串操作指令，当计数器%ecx的值不为零是就一直重复后面的串操作指令。后面是insl指令，那么被重复调用的指令就是insl指令
    7cd9:	5f                   	pop    %edi
    7cda:	5d                   	pop    %ebp
    7cdb:	c3                   	ret 
    ;执行完这个函数之后，回到readseg函数
    ;循环完之后，回到bootmain，接着执行：
    if (ELFHDR->e_magic != ELF_MAGIC)
    7d2b:	83 c4 0c             	add    $0xc,%esp
    7d2e:	81 3d 00 00 01 00 7f 	cmpl   $0x464c457f,0x10000  ;验证ELF头
    7d35:	45 4c 46 
    7d38:	75 37                	jne    7d71 <bootmain+0x5c>
    ;验证完往下走：
    ph = (struct Proghdr *) ((uint8_t *) ELFHDR + ELFHDR->e_phoff);
    7d3a:	a1 1c 00 01 00       	mov    0x1001c,%eax  ;这里应该是放了一个偏移
	eph = ph + ELFHDR->e_phnum;
    7d3f:	0f b7 35 2c 00 01 00 	movzwl 0x1002c,%esi   ;又是一个偏移吧
	ph = (struct Proghdr *) ((uint8_t *) ELFHDR + ELFHDR->e_phoff);
    7d46:	8d 98 00 00 01 00    	lea    0x10000(%eax),%ebx  ;得到ph即起始物理地址
	eph = ph + ELFHDR->e_phnum;
    7d4c:	c1 e6 05             	shl    $0x5,%esi  ;左移五位，求大小
    7d4f:	01 de                	add    %ebx,%esi  ;得到结束的地址
    ;接着是几个for循环
    for (; ph < eph; ph++)  ;这个循环把内核的每一段放到内存
    7d51:	39 f3                	cmp    %esi,%ebx
    7d53:	73 16                	jae    7d6b <bootmain+0x56>
		readseg(ph->p_pa, ph->p_memsz, ph->p_offset);
    7d55:	ff 73 04             	pushl  0x4(%ebx)
    7d58:	ff 73 14             	pushl  0x14(%ebx)  ;参数压栈
	for (; ph < eph; ph++)
    7d5b:	83 c3 20             	add    $0x20,%ebx  ;指向下一个
		readseg(ph->p_pa, ph->p_memsz, ph->p_offset);
    7d5e:	ff 73 ec             	pushl  -0x14(%ebx)
    7d61:	e8 76 ff ff ff       	call   7cdc <readseg>
	for (; ph < eph; ph++)
    7d66:	83 c4 0c             	add    $0xc,%esp
    7d69:	eb e6                	jmp    7d51 <bootmain+0x3c>  ;循环
	((void (*)(void)) (ELFHDR->e_entry))();
    7d6b:	ff 15 18 00 01 00    	call   *0x10018  ;循环结束后，把控制权交给操作系统内核
}
```

**ELF头结构**

```
typedef struct
{
  unsigned char	e_ident[EI_NIDENT];	/* Magic number和其它信息 */
  Elf32_Half	e_type;			/* Object file type */
  Elf32_Half	e_machine;		/* Architecture */
  Elf32_Word	e_version;		/* Object file version */
  Elf32_Addr	e_entry;		/* Entry point virtual address */
  Elf32_Off	e_phoff;		/* Program header table file offset */
  Elf32_Off	e_shoff;		/* Section header table file offset */
  Elf32_Word	e_flags;		/* Processor-specific flags */
  Elf32_Half	e_ehsize;		/* ELF header size in bytes */
  Elf32_Half	e_phentsize;		/* Program header table entry size */
  Elf32_Half	e_phnum;		/* Program header table entry count */
  Elf32_Half	e_shentsize;		/* Section header table entry size */
  Elf32_Half	e_shnum;		/* Section header table entry count */
  Elf32_Half	e_shstrndx;		/* Section header string table index */
} Elf32_Ehdr;
```



### 回答问题



1. 处理器在什么时候开始执行32位代码?究竟是什么导致了从16位模式到32位模式的转换?

   计算机最开始是运行于实模式的，此时是16bit，在boot.s中有一句`ljmp $PROT_MODE_CSEG, $protcseg`将工作模式修改为保护模式，此时位于32位工作模式。

2. boot loader执行的最后一条指令是什么?它刚刚加载的内核的第一条指令是什么?

   最后一条指令是((void (*)(void)) (ELFHDR->e_entry))()，跳转到操作系统内核程序的起始指令处。

   内核的第一条指令是movw $0x1234, 0x472。

3. 内核的第一条指令在哪里?

   位于/kern/entry.S文件中。

4. boot loader如何决定为了从磁盘获取整个内核必须读取多少扇区?它从哪里找到这些信息?

   Program Header Table中有操作系统有关段、扇区的信息。这个表中的每个表项分别对应操作系统的一个段。并且每个表项的内容包括这个段的大小，段起始地址偏移等等信息。所以我们可以通过表项所提供的信息来确定内核占用多少个扇区。

   这个表存放在操作系统内核映像文件的ELF头部信息中。

## Loading the kernel

### 练习4

​	阅读关于C语言中指针编程的文章。在K&amp;R中从5.1(指针和地址)读到5.5(字符指针和函数)。然后下载指针的代码。c，运行它，并确保理解所有打印值的来源。注意，确保了解打印的第1行和第6行中的指针地址来自何处，打印的第2行到第4行中的所有值是如何到达那里的，以及为什么打印的第5行中的值似乎是损坏的。

​	我们下载这个程序并运行它。

```C
#include <stdio.h>
#include <stdlib.h>

void
f(void)
{   
    int a[4];    //数组
    int *b = malloc(16);    //指针并分配内存空间
    int *c;   //指针
    int i;
    //a输出的是数组的首地址，b输出的是内存起始空间，c是未定义指针
    printf("1: a = %p, b = %p, c = %p\n", a, b, c);

    c = a; //此时a和c同时指向a的内存地址，
    for (i = 0; i < 4; i++)  //给a的四个数组元素赋值100,101,102,103 
		a[i] = 100 + i;   
    c[0] = 200; //此时a的第一个元素=200，c的内存数据也为200
    printf("2: a[0] = %d, a[1] = %d, a[2] = %d, a[3] = %d\n",
	   a[0], a[1], a[2], a[3]);  //即200,101,102,103
    
    //以下是访问数组的三种不同方法
    c[1] = 300;  //a[1]=300
    *(c + 2) = 301;  //a[2]=301
    3[c] = 302;   //a[3]=302
    printf("3: a[0] = %d, a[1] = %d, a[2] = %d, a[3] = %d\n",
	   a[0], a[1], a[2], a[3]);

    c = c + 1; //指向下一个元素a[1]
    *c = 400;  //a[1]=400
    printf("4: a[0] = %d, a[1] = %d, a[2] = %d, a[3] = %d\n",
	   a[0], a[1], a[2], a[3]);

    c = (int *) ((char *) c + 1);//把c强转为char型，然后+1再转回int，因为char和int大小不同，所以c在赋值时会影响到a[1]及后面的元素
    *c = 500;//四个字节大小，影响了a[1]和a[2]两个元素
    printf("5: a[0] = %d, a[1] = %d, a[2] = %d, a[3] = %d\n",
	   a[0], a[1], a[2], a[3]);

    b = (int *) a + 1;  //b=a[0]的内存地址+四个字节（int的大小）
    c = (int *) ((char *) a + 1);  //c=a[0]的内存地址+1个字节（char的大小）
    printf("6: a = %p, b = %p, c = %p\n", a, b, c);
}

int
main(int ac, char **av)
{
    f();
    return 0;
}
```

​	原理在代码的注释上，下面是运行结果：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd8j0yvo1yj30jr04xmxx.jpg)

​		要理解boot/main.c，需要知道ELF二进制文件是什么。当我们编译并链接一个C程序(如JOS内核)时，编译器将每个C源代码('. C ')文件转换成一个对象('.o')文件，目标文件中包含的是机器直接能够执行的机器指令。然后，链接器将所有编译后的目标文件组合成一个单一的二进制映像，比如obj/kern/kernel，在本例中是ELF格式的二进制映像，ELF格式代表“可执行和可链接格式”。

​		在6.828中，我们可以将ELF可执行文件看作是一个带有加载信息的头文件，后面是几个程序节，每个程序节是一个连续的代码块或数据块，目的是在指定的地址加载到内存中。boot loader不修改代码或数据;它将它加载到内存中并开始执行它。

​		ELF二进制文件以一个固定长度的ELF头开始，然后是一个可变长度的程序头，列出要加载的每个程序段。这些ELF头文件的C定义在inc/ ELF .h中。我们感兴趣的部分有:

   	 .text:程序的可执行指令。

​		.rodata:只读数据，如C编译器生成的ASCII字符串常量。(不过，我们不会设置硬件来禁止写入。)

​		.data: data部分保存程序的初始化数据，比如用初始化器声明的全局变量，比如int x = 5;。

​		当链接器计算程序的内存布局时，它为未初始化的全局变量保留空间，比如int x;，在一个称为.bss的部分中，该部分紧随着内存中的.data。C要求“未初始化的”全局变量以0值开始。因此，不需要在ELF二进制文件中存储.bss的内容;相反，链接器只记录.bss部分的地址和大小。加载程序或程序本身必须将.bss部分归零。

​		通过键入以下命令，检查内核可执行文件中所有部分的名称、大小和链接地址的完整列表:

`objdump -h obj/kern/kernel`

我们看一下这部分的信息：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd8kzb1rxoj30n10ho419.jpg)

​		请特别注意.text部分的“VMA”(或链接地址)和“LMA”(或加载地址)。段的加载地址是应该将该段加载到内存中的内存地址。节的链接地址是节期望从中执行的内存地址。链接器以各种方式对二进制文件中的链接地址进行编码，例如，当代码需要全局变量的地址时，如果从一个没有链接的地址执行二进制文件，通常会导致二进制文件无法工作。

​		通常，链接地址和加载地址是相同的。通过下面指令查看引导加载程序的.text部分:

`objdump -h obj/boot/boot.out`

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd8l6fakprj30ll09udh3.jpg)

​		对比VMA和LMA,确实是相同的。

​		引导加载程序使用ELF程序头来决定如何加载节。程序头指定ELF对象的哪些部分要加载到内存中，以及每个部分的目标地址。使用如下命令:

`objdump -x obj/kern/kernel`

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd8lreyaw6j30o90dg40h.jpg)

​		然后，在objdump的输出中，将程序头列在“程序头”之下。ELF对象中需要加载到内存中的区域是那些被标记为“LOAD”的区域。每个程序头的其他信息都是给定的，比如虚拟地址(“vaddr”)、物理地址(“paddr”)和加载区域的大小(“memsz”和“filesz”)。

​		回到boot/main.c。每个程序头的ph->p_pa字段包含段的目标物理地址(在本例中，它实际上是一个物理地址，尽管ELF规范对该字段的实际含义含糊其辞)。

​		BIOS从地址0x7c00开始将boot sector加载到内存中，所以这是boot sector的加载地址。这也是boot sector执行的位置，所以这也是它的链接地址。我们通过将-Ttext 0x7C00传递给boot/Makefrag中的链接器来设置链接地址，这样链接器将在生成的代码中生成正确的内存地址。

###练习5

​		再次跟踪boot loader的前几条指令，找到第一条指令满足：修改了boot loader的链接地址，这个指令就会出现错误。然后将boot/Makefrag中的链接地址修改，运行make clean，用make重新编译，然后再次跟踪boot loader，看看会发生什么。别忘了把链接地址改回来。

​		开始时链接地址和加载地址是相同的，BIOS从0x7c00处开始加载。所以我们要修改的就是这个值。然后我们打开boot/makefrag这个文件：

```
$(OBJDIR)/boot/boot: $(BOOT_OBJS)
	@echo + ld boot/boot
	$(V)$(LD) $(LDFLAGS) -N -e start -Ttext 0x7C00 -o $@.out $^
	$(V)$(OBJDUMP) -S $@.out >$@.asm
	$(V)$(OBJCOPY) -S -O binary -j .text $@.out $@
	$(V)perl boot/sign.pl $(OBJDIR)/boot/boot	
```

​		有这样一段，我们把-Ttext 0x7C00这里修改为0x7e00。执行make clean，再执行make。然后我们发现汇编代码里面

```
.globl start
start:
  .code16                     # Assemble for 16-bit mode
  cli                         # Disable interrupts
    7e00:	fa                   	cli    
  cld                         # String operations increment
    7e01:	fc                   	cld  
```

地址变成了我们修改之后的值。然后我们跟踪一下boot loader：

![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd8n7jlz8jj30ot0detbe.jpg)

发现坏了，没法跟踪。  那就把地址改回来吧。

​		查看内核的加载和链接地址。与boot loader不同，这两个地址是不同的:内核告诉boot loader以低地址将其加载到内存中，但它期望从高地址执行。在下一节中，我们将深入研究如何实现这一功能。

​		除了节信息之外，ELF头中还有一个对我们很重要的字段，它就是e_entry。该字段保存程序入口点的链接地址:程序文本部分中的内存地址，程序应该从这里开始执行。你可以看到入口点。

`objdump -f obj/kern/kernel`

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd8vag637oj30k403qt8y.jpg)

​		我们现在应该知道boot/main.c中最小的ELF加载程序。它将内核的每个部分从磁盘上的加载地址读入内存，然后跳转到内核的入口点。

### 练习6

​		我们可以使用GDB的x命令来检查内存。GDB手册有完整的细节，但是现在，只要知道命令x/Nx ADDR在ADDR打印N个内存单词就足够了。

​		重置机器(退出QEMU/GDB并重新启动它们)。在BIOS进入boot loader的地方检查内存0x00100000中的8个字，然后在boot loader进入内核的地方再检查一次。它们为什么不同?第二个断点是什么?

在一开始，我们读取0x00100000处的内存：

![3.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd8vt5zx5mj30np02274f.jpg)

在boot loader进入内核后再读取一次：

![2.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd8vsnjsa3j30mw01vaa5.jpg)

发现内存中的值有变化，原因是前面我们分析过bootmain函数，他会把各个程序段存放在内存，这样我们读取到的就是内核中某一个段的内容。

第二次读取的内容，由于程序入口是0x0010000c，在我们读取的内存中，对比上面我们objdump的内容，这里面存放的应该是.text节部分。

# 加载地址和链接地址

​		加载地址（存储地址）：加载地址是指代码存储所在的物理地址，由于ARM总是从0开始取值，即PC初始值为0，所以加载地址必须对应0地址，程序才能正确启动执行，之后才可以进行跳转，比如设置PC等于一个子程序的入口地址，而这个入口地址可能在rom中也可能在ram中。

​		链接地址（运行地址）：运行地址是链接器根据链接文件中指定的链接地址作为程序运行的起始地址，（作用）将程序中所有指令地址按照相对于这个起始地址的位置进行赋值，与程序实际运行时地址不一定相同，因为实际的程序运行起始地址总是0，而链接地址不一定为0，决定于程序需要在ROM还是RAM中运行）；如果链接地址与加载地址设为相同，则所有寻址地址在ROM上都有对应的指令存在，也就可以正常执行；但如果链接地址与加载地址不同，则所有指令的地址是根据链接地址确定的，在ROM上不存在与之对应的代码入口（即使链接地址正好还处于ROM代码中，接下来执行的指令也将是不正确的和存在偏移的）
​		加载地址是程序实际执行时的真实起始地址且必须为0；而链接地址的作用就是在链接过程中以链接地址为基准，确定程序中所有指令的相对位置，加载地址与链接地址相同时，该相对位置处才存在对应的指令，否则该相对位置处将不存在正确指令

# part3： the kernel

## 虚拟内存

​		在运行boot loader时，boot loader中的链接地址（虚拟地址）和加载地址（物理地址）是一样的。但是内核的链接地址(由objdump打印)和它的加载地址之间存在(相当大的)差异。

​		操作系统内核通常喜欢在非常高的虚拟地址(比如0xf0100000)上进行链接和运行，以便将处理器虚拟地址空间的较低部分留给用户程序使用。

​		许多机器在地址0xf0100000处没有任何物理内存，因此我们不能指望能够将内核存储在那里。相反，我们将使用处理器的内存管理硬件将虚拟地址0xf0100000(内核代码期望在其上运行的链接地址)映射到物理地址0x00100000(boot loader将内核加载到物理内存中)。

​		实际上，我们将映射PC的整个底层256MB物理地址空间，从物理地址0x00000000到0x0fffffff，分别映射到虚拟地址0xf0000000到0xffffffff。

​		现在，我们只映射第一个4MB的物理内存，这足够让我们启动和运行。我们使用kern/entrypgdir.c中手工编写的、静态初始化的页面目录和页面表来实现这一点。kern/entry.S设置CR0_PG标志，内存引用被视为物理地址(严格地说，它们是线性地址，但是boot/boot.S建立了一个从线性地址到物理地址的恒等映射，我们永远不会改变它)。设置CR0_PG之后，内存引用就是虚拟地址，由虚拟内存硬件转换为物理地址。entry_pgdir将范围为0xf0000000到0xf0400000的虚拟地址转换为物理地址0x00000000到0x00400000，以及将虚拟地址0x00000000到0x00400000转换为物理地址0x00000000到0x00400000。任何不在这两个范围内的虚拟地址都会导致一个硬件异常。

###练习7

​		使用QEMU和GDB跟踪到JOS内核，并在movl %eax %cr0处停止。检查0x00100000和0xf0100000处的内存。现在，使用stepi GDB命令单步执行该指令。再次检查0x00100000和0xf0100000处的内存。确保你明白刚刚发生了什么。

​		在新映射建立之后，如果映射没有就位，第一个不能正常工作的指令是什么?在kern/entry.S中注释掉movl %eax， %cr0，验证一下。

​		我们在0x100025处下断点，也就是movl %eax %cr0这个指令的地址，然后查看0x00100000和0xf0100000处的内存，内存数据不相同，再执行si，单步执行完movl指令，再查看0x00100000和0xf0100000处的内存，发现内存数据相等：

​		![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd9jttqqwaj30nl0cvjsq.jpg)

​		原因是这句代码的作用是开启分页，开启分页前，高地址的内容未映射，开启分页之后，虚拟地址映射到了物理内存，所以内存的内容是一样的。

​		把这句代码注释掉，重新make一下，下断点到0x100025，单步执行发现报错：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd9k847toij30nn0axwfe.jpg)

回到qemu这边报错：尝试在RAM或ROM之外的0xf010002c处执行代码，访问的地址超出内存了。

![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd9k8s5o2pj30md0420tc.jpg)

## 格式化打印到控制台

​		大多数人认为printf()这样的函数是理所当然的，有时甚至认为它们是C语言的“原语”。但是在操作系统内核中，我们必须自己实现所有的I/O。

​		通读kern / printf.c, lib / printfmt.c, kern /console.c，并确保你了解他们的关系。在以后的实验室中，我们将清楚地了解为什么printfmt.c位于lib目录中。

### 练习8

​		我们省略了一小段代码——使用“%o”形式的模式打印八进制数所必需的代码。查找并填充这段代码。

​		先看一下这段代码：

```
// (unsigned) octal
		case 'o':
			// Replace this with your code.
			putch('X', putdat);
			putch('X', putdat);
			putch('X', putdat);
			break;
			
//填充代码：
case 'o':
     // Replace this with your code.
     num = getuint(&ap, lflag);  //从ap指向的可变字符串中获取输出的值
     base = 8;  //设置基数为8
     goto number;
```

然后执行make grade验证

1. 解释printf.c和console.c之间的接口。具体来说，console.c导出什么功能?printf.c是如何使用这个函数的?

   ​	console.c导出了cputchar,getchar等函数，printf.c调用了console.c中的cputchar函数。

2. 解释console.c中的

   ```
   if (crt_pos >= CRT_SIZE) {
   	int i;
       memmove(crt_buf, crt_buf + CRT_COLS, (CRT_SIZE - CRT_COLS) * sizeof(uint16_t));
       for (i = CRT_SIZE - CRT_COLS; i < CRT_SIZE; i++)
       	crt_buf[i] = 0x0700 | ' ';
       crt_pos -= CRT_COLS;
   }
   ```

   这个函数实现了滚屏的功能。文本模式下一页屏幕最多显示25*80个字符，超出时，需要将2~25行往上提一行，memmove把内容向上移动一行，最后一行用空格填充。

3. 跟踪以下代码：

   ```
   int x = 1, y = 3, z = 4;
   cprintf("x %d, y %x, z %d\n", x, y, z);
   ```

   Q:在调用cprintf()时，fmt指向什么?ap指向什么?

   fmt指向的是参数中的格式字符串，也就是"x %d, y %x, z %d\n"，而ap指向fmt的后一个参数地址

   Q:列出(按执行顺序)对cons_putc、va_arg和vcprintf的每个调用。对于cons_putc，也列出它的参数。对于va_arg，列出调用之前和之后ap指向的内容。对于vcprintf，列出它的两个参数的值。

   调用va_arg前，ap存放待显示的x,y,z三个参数的内容1,3,4，调用va_arg之后，待显示的参数剩了y和z，即3,和4。

   vcprintf的两个参数是格式字符串fmt和参数列表ap。

4. 运行以下代码：

   ```
    unsigned int i = 0x00646c72;
    cprintf("H%x Wo%s", 57616, &i);
   ```

Q：输出是什么?按照前面的练习一步一步地解释这个输出是如何得到的。

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd9tb1hqihj305000ujr6.jpg)

​		把代码添加到kern\monitor.c中，重新make，得到输出He110 World。第一个参数%x输出的是16进制的57616也就是e110，第二个参数是%s的&i，是变量i的内存地址，题目中说x86是小端序，也就是最高位字节存放在最高位字节地址上，int类型的i有四个字节，则分别存放了0x72,0x6c,0x64,0x00，对应字符为rld。字符串拼接起来就成了输出是He110 World。

Q：输出取决于x86是little-endian这一事实。如果x86是big-endian，为了得到相同的输出，您会将i设置为什么?您需要将57616更改为不同的值吗?

​		如果是大端序，57616不需要改变，i则改为0x726c6400。

5. 在下面的代码中，'y='后面要打印什么?(注意:答案不是一个特定的值)为什么会这样?

   ```
    cprintf("x=%d y=%d", 3);
   ```

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gd9tmchrjvj307z00nq2q.jpg)

y输出的是x地址往上4个字节的内容。

6. 假设GCC改变了它的调用约定，以声明的顺序将参数推入栈中，最后一个参数最后被推入。你将如何改变cprintf或它的接口，使它仍然有可能传递一个可变数量的参数?

​       放一个表示参数数量的变量。

## 栈

​		在这个实验的最后练习中,我们将更详细地探讨C语言使用x86上的堆栈的方式,并在这一过程中写一个新的kernel monitor,输出一个堆栈回溯。

### 练习9

确定内核初始化堆栈的位置，以及堆栈在内存中的确切位置。内核如何为它的堆栈保留空间?堆栈指针初始化后指向该保留区域的哪个“端”?

初始化堆栈的位置：

	movl	$0x0,%ebp			# nuke frame pointer
	# Set the stack pointer
	movl	$(bootstacktop),%esp

堆栈的位置：

```
.data
###################################################################
# boot stack
###################################################################
	.p2align	PGSHIFT		# force page alignment
	.globl		bootstack
bootstack:
	.space		KSTKSIZE
	.globl		bootstacktop   
bootstacktop:
```

这里给出了bootstacktop以及栈的大小bootstacktop，为32kb，在内存中声明了堆栈的空间。

我们查看kernel.asm：

```
# Set the stack pointer
	movl	$(bootstacktop),%esp
f0100034:	bc 00 00 11 f0       	mov    $0xf0110000,%esp
```

得到栈顶是0xf0110000，所以栈的位置是从0xf0110000往下的32kb，指针指向的是栈顶的位置。

# part 3

##栈

​		x86堆栈指针(esp寄存器)指向当前正在使用的堆栈的最低位置。该位置以下为堆栈保留的区域中，所有内容都是空闲的。将一个值压入堆栈时，堆栈指针要减1，然后将该值写入堆栈指针指向的位置。从堆栈弹出一个值时，先读取堆栈指针的值，然后增加堆栈指针。在32位模式下，堆栈的操作都是32位的，esp的值总是能被4整除。各种x86指令(如call)都是“硬连接”的，以使用堆栈指针寄存器。

​		ebp(基本指针)寄存器记录每一个程序的栈帧信息。在进入C函数时，要运行的代码通常通过将前一个函数的ebp的值压入堆栈来保存它，然后在函数执行期间将当前的esp值复制到ebp中。如果所有的功能在程序遵守这样的规则,在程序的执行期间的任意一点时,可以通过在堆栈中保存的一系列ebp寄存器的值来回溯，弄清楚是怎样的一个函数调用序列使我们的程序运行到当前的这个点。这个功能特别有用，例如，当一个特定的函数由于传递了错误的参数而导致assert失败或恐慌时，但是我们不确定是谁传递了错误的参数。堆栈回溯可以让我们找到有问题的函数。

### 练习10

​		Q：要熟悉x86上的C调用约定，在obj/kern/kernel.asm中查找test_backtrace函数的地址。在这里设置一个断点，并检查内核启动后每次调用它时发生了什么。test_backtrace的每个递归嵌套在堆栈上压了多少个32位的信息，这些信息是什么?

```
f0100040 <test_backtrace>:
#include <kern/console.h>

// Test the stack backtrace function (lab 1 only)
void
test_backtrace(int x)
{
	cprintf("entering test_backtrace %d\n", x);
	if (x > 0)
		test_backtrace(x-1);
	else
		mon_backtrace(0, 0, 0);
	cprintf("leaving test_backtrace %d\n", x);
}
```

​		我们找到这个函数的地址，在asm文件中扣出c代码，发现这是一个循环调用的函数，打印的信息就像栈一样先进后出，然后我们下个断点：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdbwpyult4j30j409yt9h.jpg)

​		设置断点后运行到test_backtrace处停下，查看此时寄存器信息，此时esp存放的是i386_init的返回地址0xf010ffdc，ebp则是栈帧的顶部。然后往下走si：

​		mov %esp,%ebp这句将esp的值赋给ebp，这也正是上面说的回溯应该遵循的准则

​		push   %esi  将esi的值压栈，

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdbxzd82sjj30h705laab.jpg)

​		push   %ebx  将ebx的值压栈![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdbxyjcrdlj30gx05ower.jpg)

执行完第一次循环时

![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdby1m8c26j30kw05ogm2.jpg)

然后来到第二次循环时的断点，x=4时：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdby2myminj30is083q3d.jpg)

x=3时：

![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdby3b4z3nj30ge05vaab.jpg)

可见，每次循环时在堆栈上压了两个32位的信息。

​		上面的练习给我们提供实现堆栈回溯函数所需的信息，该函数称为mon_backtrace()。这个函数的原型已经在kern/monitor.c中声明了。你可以用C来实现，但是你会发现inc/x86.h中的read_ebp()函数很有用。将这个新函数挂接到内核监视器的命令列表中，以便用户可以交互地调用它。

​		backtrace函数应该以如下格式显示函数调用帧的列表:

```
Stack backtrace:
  ebp f0109e58  eip f0100a62  args 00000001 f0109e80 f0109e98 f0100ed2 00000031
  ebp f0109ed8  eip f01000d6  args 00000000 00000000 f0100058 f0109f28 00000061
  ...
```

​		每行包含一个ebp、eip和args。ebp值表示该函数使用的堆栈基指针。所列出的eip值是函数的返回指令指针。返回指令指针通常指向调用指令之后的指令。最后，args之后列出的5个十六进制值是函数的前5个参数，这些参数在调用函数之前会被压入堆栈。当然，如果调用函数的参数少于5个，那么这5个值并不都有用。

几点要注意的：

​		如果int* p = (int*)100，那么(int)p + 1和(int)(p + 1)是不同的数字:第一个是101，第二个是104。在向指针添加整数时(如第二种情况)，该整数将隐式地乘以指针指向的对象的大小。

​		p[i]被定义为与*(p+i)相同，指的是p指向的内存中的第i个对象。当对象大于一个字节时，上面的加法规则可以帮助这个定义工作。

​		&p[i]与(p+i)相同，产生由p指向的内存中的第i个对象的地址。

​		尽管大多数C程序从不需要在指针和整数之间进行转换，但是操作系统经常需要这样做。无论何时看到涉及内存地址的加法，都要问问自己它是整数加法还是指针加法，并确保所添加的值相乘是否正确。

### 练习11

​		Q：实现上面指定的回溯函数。使用与示例中相同的格式，否则评分脚本会混淆。

​		如果您使用read_ebp()，请注意GCC可能会在mon_backtrace()的函数序言之前生成调用read_ebp()的“优化”代码，这将导致不完整的堆栈跟踪(最近一次函数调用的堆栈帧丢失)。虽然我们试图禁用导致这种重新排序的优化，但是您可能希望检查mon_backtrace()的程序集，并确保在函数序言之后调用read_ebp()。

​		 这个程序的功能就是要显示当前正在执行的程序的栈帧信息。包括当前的ebp寄存器的值（该子程序的栈帧的最高地址），eip的值（子程序执行完成之后要返回调用它的子程序时，下一个要执行的指令地址）。args的值（子程序接受的来自调用它的子程序传递给它的输入参数）。

​		我们要做的就是根据ebp推导出这些信息:

```
int mon_backtrace(int argc, char **argv, struct Trapframe *tf)
{
	// Your code here.
	int i;
	uint32_t eip;
	uint32_t* ebp = (uint32_t *)read_ebp();

	while (ebp) {
		eip = *(ebp + 1);
		cprintf("ebp %x eip %x args", ebp, eip);
		uint32_t *args = ebp + 2;
		for (i = 0; i < 5; i++) {
			uint32_t argi = args[i];
			cprintf(" %08x ", argi);
		}
		cprintf("\n");
		ebp = (uint32_t *) *ebp;
	}
	return 0;
}
```

运行之后的输出：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdcbg369o1j30ry0eqwh3.jpg)

make一下：

![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdcbgv0yggj30hn09ngm0.jpg)

​		此时，backtrace函数应该可以提供导致执行mon_backtrace()函数的函数地址。但是，在实践中，我们通常希望知道与这些地址对应的函数名。

​		为了实现此功能，我们提供了函数debuginfo_eip()，它在符号表中查找eip并返回该地址的调试信息。这个函数在kern/kdebug.c中定义。

### 练习12

​		修改mon_backtrace()以显示每个eip对应的函数名、源文件名和行号。在debuginfo_eip中，_STAB_*来自哪里?这个问题的答案很长;为了帮助你找到答案，这里有一些你可能想要做的事情

查看kern/kernel.ld文件__STAB_ *

运行objdump -h obj/kern/kernel

运行objdump -G obj/kern/kernel

运行gcc -pipe -nostdinc -O2 -fno-builtin -I. -MD -Wall -Wno-format -DJOS_KERNEL -gstabs -c -S kern/init.c，然后看init.S。

查看boot loader是否将符号表作为加载内核二进制文件的一部分加载到内存中

在kern/kernel.ld文件中，有关STAB的相关代码：

```
/* Include debugging information in kernel memory */
	.stab : {
		PROVIDE(__STAB_BEGIN__ = .);
		*(.stab);
		PROVIDE(__STAB_END__ = .);
		BYTE(0)		/* Force the linker to allocate space
				   for this section */
	}
	
.stabstr : {
    PROVIDE(__STABSTR_BEGIN__ = .);
    *(.stabstr);
    PROVIDE(__STABSTR_END__ = .);
    BYTE(0)     /* Force the linker to allocate space
               for this section */
```

__STAB_BEGIN__和__STAB_END__是.stab 的开始和结束，__STABSTR_BEGIN__和__STABSTR_END__是.stabstr的开始和结束，这两个都在kern/kern.ld中定义。

执行objdump -h obj/kern/kernel：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdcycjfyxkj30lf0h9q5l.jpg)

给出了每个段的大小、加载地址、链接地址等信息。

执行objdump -G obj/kern/kernel：

![屏幕截图1.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdcyezejryj30mz0fedhc.jpg)

给出了.stab节的一些信息。Symnum是符号索引，n_type是符号类型，SLINE指在text段中的行号，n_othr目前没被使用，其值固定为0，n_desc表示在文件中的行号，n_value表示地址。

运行gcc -pipe -nostdinc -O2 -fno-builtin -I. -MD -Wall -Wno-format -DJOS_KERNEL -gstabs -c -S kern/init.c，然后看init.S，可以看到最开始加载了一堆符号表。

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdcyl2bz9uj30oy0jsq65.jpg)

其中第2行是描述源文件信息的，文件名是 init.c，类型是N_SO（100），后面的desc=2表示C语言文件，.Ltext0为文件对应代码区的开始地址。

Q：通过插入对stab_binsearch的调用来查找地址的行号，完成debuginfo_eip的实现。

```
// Search within [lline, rline] for the line number stab.
// If found, set info->eip_line to the right line number.
// If not found, return -1.
//
// Hint:
//	There's a particular stabs type used for line numbers.
//	Look at the STABS documentation and <inc/stab.h> to find
//	which one.
// Your code here.
stab_binsearch(stabs, &lline, &rline, N_SLINE, addr);
	if (lline <= rline) {
   		 info->eip_line = stabs[lline].n_desc;
	} 
	else {
  	  return -1;
	}
```

​		向内核监视器添加一个backtrace命令，并扩展mon_backtrace的实现，以调用debuginfo_eip并为表单的每个堆栈帧打印一行:

```
K> backtrace
Stack backtrace:
  ebp f010ff78  eip f01008ae  args 00000001 f010ff8c 00000000 f0110580 00000000
         kern/monitor.c:143: monitor+106
  ebp f010ffd8  eip f0100193  args 00000000 00001aac 00000660 00000000 00000000
         kern/init.c:49: i386_init+59
  ebp f010fff8  eip f010003d  args 00000000 00000000 0000ffff 10cf9a00 0000ffff
         kern/entry.S:70: <unknown>+0
K> 
```

每行给出堆栈框架的eip文件的文件名和该文件中的行，后面是函数名和eip从函数的第一条指令开始的偏移量(例如，monitor+106表示返回的eip是monitor开始后的106字节)。

确保将文件和函数名打印在单独的行上，以避免混淆评分脚本。

提示:printf格式字符串提供了一种简单(尽管有些晦涩)的方法来打印非空终止的字符串，就像STABS表中的那些字符串一样。printf (" %。*s"，长度，字符串)打印字符串的最大长度字符。查看printf手册页，了解其工作原理。

完整的函数：

```
int
mon_backtrace(int argc, char **argv, struct Trapframe *tf)
{
	// Your code here.
        int i;
	uint32_t eip;
	uint32_t* ebp = (uint32_t *)read_ebp();
	while (ebp) {
	//打印ebp,eip和五个参数
		eip = *(ebp + 1);
		cprintf("ebp %x eip %x args", ebp, eip);
		uint32_t *args = ebp + 2;
		for (i = 0; i < 5; i++) {
			uint32_t argi = args[i];
			cprintf(" %08x ", argi);
		}
		cprintf("\n");
		//打印文件名等信息
		struct Eipdebuginfo debug_info;
		debuginfo_eip(eip, &debug_info);
		cprintf("\t%s:%d: %.*s+%d\n",
			debug_info.eip_file,
			debug_info.eip_line,
			debug_info.eip_fn_namelen,
			debug_info.eip_fn_name, 
			eip - debug_info.eip_fn_addr);
		//更新ebp
		ebp = (uint32_t *) *ebp;
	}
	return 0;
}
```

函数运行效果如下：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdd5bnsruoj30os09zmzd.jpg)

make一下：

![屏幕截图.jpg](http://ww1.sinaimg.cn/large/005KQQDely1gdd5afoifoj30cm04xgls.jpg)

这就是lab1的最后一道练习题。

